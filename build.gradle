plugins {
    id "eclipse"
    id "idea"
    id "io.sdkman.vendors"              version "1.1.1"     apply false
    id "com.jfrog.bintray"              version "1.8.0"     apply false
    id "com.github.kt3k.coveralls"      version "2.8.2"     apply false
    id "org.sonarqube"                  version "2.6.2"     apply false
    id 'com.github.ben-manes.versions'  version '0.17.0'
}

// common variables
ext {
    asciidoctorjVersion = '1.5.6'
    asciidoctorjDiagramVersion = '1.5.4.1'
    commonsIoVersion = '2.6'
    commonsConfigurationVersion = '1.10'
    commonsLangVersion = '3.7'
    commonsVfs2Version = '2.2'
    args4jVersion = '2.33'
    freemarkerVersion = '2.3.27-incubating'
    junit4Version = '4.12'
    flexmarkVersion = '0.28.38'
    jettyServerVersion = '9.2.24.v20180105'
    orientDbVersion = '2.2.30'
    groovyVersion = '2.4.13'
    slf4jVersion = '1.7.25'
    logbackVersion = '1.2.3'
    assertjCoreVersion = '2.9.0'
    thymeleafVersion = '3.0.9.RELEASE'
    jsonSimpleVersion = '1.1.1'
    jade4jVersion = '1.2.7'
    mockitoVersion = '2.13.0'
    jsoupVersion = '1.10.3'

    isTravis = (System.getenv("TRAVIS") == "true")
    isTravisPullRequest = (System.getenv("TRAVIS_PULL_REQUEST")) != "false"
    pullRequestId = System.getenv("TRAVIS_PULL_REQUEST")
    hasGithub = System.getenv("GITHUBTOKEN") && System.getenv("GITHUBREPO")
    hasSonar = System.getenv("SONARORG") && System.getenv("SONARLOGIN")
    sonarDefaultURL = "https://sonarcloud.io"
    sonarDefaultProjectKey = "org.jbake:jbake-base:jbake-core"
    sonarURL = System.getenv("SONARHOST") ?: sonarDefaultURL
    sonarProjectKey = System.getenv("SONARPROJECTKEY") ?: sonarDefaultProjectKey
}

/**
 * Apply coveralls to the root project as we just need it here to send the
 * aggregated coverage execution data from the jacocoRootReport task
 */
apply plugin: 'com.github.kt3k.coveralls'

/**
 * Apply jacoco plugin to all projects and add jcenter as default repository
 */
allprojects {
    apply plugin: 'jacoco'

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = jacocoVersion
    }

}

/**
 * Common setup for all subprojects
 */
subprojects {

    apply plugin: 'java'

    if( JavaVersion.current().java7Compatible ) {
        apply from: "$rootDir/gradle/signing.gradle"
        // We do not publish any jars from the jbake-dist project
        if ( project.name != "jbake-dist" ) {
            apply from: "$rootDir/gradle/maven-publishing.gradle"
        }
        // bintray setup
        apply from: "$rootDir/gradle/publishing.gradle"
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
        compile "ch.qos.logback:logback-classic:$logbackVersion"
        compile "ch.qos.logback:logback-core:$logbackVersion"
        testCompile "junit:junit:$junit4Version"
        testCompile "org.assertj:assertj-core:$assertjCoreVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]?${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    test {
        jvmArgs '-XX:MaxPermSize=2048m', '-XX:MaxDirectMemorySize=512m', '-Dorientdb.installCustomFormatter=false=false'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    jacocoTestReport.dependsOn test
}

task jacocoMerge(type: JacocoMerge) {
    description 'Merge all testreport execution data from subprojects excluding jbake-dist'
    dependsOn subprojects.test
    executionData subprojects.findAll{it.name!="jbake-dist"}.jacocoTestReport.executionData
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn jacocoMerge

    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = jacocoMerge.executionData

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

task testReport(type: TestReport) {
    description "Generate an aggregated Testreport for all projects"

    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'

    dependsOn jacocoRootReport
    // Skip Task if not run on CI Server
    onlyIf { System.env.'CI' }
}
