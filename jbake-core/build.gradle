import java.text.SimpleDateFormat

plugins {
    id "org.sonarqube" version "2.6.2"
}

ext {
    isTravis = (System.getenv("TRAVIS") == "true")
    isTravisPullRequest = (System.getenv("TRAVIS_PULL_REQUEST")) != "false"
    pullRequestId = System.getenv("TRAVIS_PULL_REQUEST")
}

sonarqube {
    properties {
        property "sonar.host.url", System.getenv('SONAR_HOST')
        property "sonar.organization", System.getenv('SONARORG')
        property "sonar.login", System.getenv('SONARLOGIN')
        property "sonar.projectKey", "org.jbake.ancho:example:jbake"

        if ( isTravis && isTravisPullRequest ) {
            property "sonar.analysis.mode", "preview"
            property "sonar.github.oauth", System.getenv("GITHUBTOKEN")
            property "sonar.github.repository", "ancho/jbake"
            property "sonar.github.pullRequest", pullRequestId
        }
    }
}


description = "The core library of JBake"

dependencies {
    compile "commons-io:commons-io:$commonsIoVersion"
    compile "commons-configuration:commons-configuration:$commonsConfigurationVersion"
    compile "org.apache.commons:commons-vfs2:$commonsVfs2Version"
    compile "org.apache.commons:commons-lang3:$commonsLangVersion"
    compile "com.googlecode.json-simple:json-simple:$jsonSimpleVersion"
    compile "com.orientechnologies:orientdb-graphdb:$orientDbVersion"
    compile "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
    compile "org.codehaus.groovy:groovy-templates:$groovyVersion"
    compile "org.freemarker:freemarker:$freemarkerVersion"
    compile "org.thymeleaf:thymeleaf:$thymeleafVersion"
    compile "de.neuland-bfi:jade4j:$jade4jVersion"
    compile "com.vladsch.flexmark:flexmark-all:$flexmarkVersion"

    // cli specific dependencies
    compile "org.eclipse.jetty:jetty-server:$jettyServerVersion"
    compile "args4j:args4j:$args4jVersion"
}

processResources {
    from("src/main/resources") {
        include 'default.properties'
        expand  jbakeVersion: project.version,
                timestamp: new SimpleDateFormat("yyyy-MM-dd HH:mm:ssa").format( new Date() )
    }
}

